{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Exemplo de uso do pacote JuMP para resolver o TSP"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using DelimitedFiles, PyCall\n",
    "\n",
    "# ========================= #\n",
    "# Carrega dados do problema #\n",
    "# ========================= #\n",
    "\n",
    "# Carregando latitude e longitude dos pontos\n",
    "LatLon = readdlm(\"data/sjcLatLon.csv\")\n",
    "LatMin = minimum(LatLon[:,1])\n",
    "LatMax = maximum(LatLon[:,1])\n",
    "LonMin = minimum(LatLon[:,2])\n",
    "LonMax = maximum(LatLon[:,2])\n",
    "\n",
    "# Carregando a matriz de distâncias\n",
    "D = readdlm(\"data/sjcEuclidiano.csv\",',')\n",
    "\n",
    "# Selecionando os n primeiros pontos da matriz de distâncias \n",
    "n = 10\n",
    "D = D[1:n,1:n]\n",
    "\n",
    "# =============================== #\n",
    "# Imprimindo o mapa com os pontos #\n",
    "# =============================== #\n",
    "\n",
    "# Importa biblioteca python \"folium\"\n",
    "flm = pyimport(\"folium\")\n",
    "\n",
    "# Cria mapa inicial com pontos\n",
    "mapa = flm.Map()\n",
    "\n",
    "# Define os limites do mapa de acordo com os pontos\n",
    "limites = [(LatMin,LonMin),(LatMax,LonMax)]\n",
    "mapa.fit_bounds(limites)\n",
    "\n",
    "# Insere os pontos no mapa\n",
    "idPonto = 1\n",
    "for i in 1:n\n",
    "    lat = LatLon[i,1]\n",
    "    lon = LatLon[i,2]\n",
    "    flm.Circle(location=[lat,lon],\n",
    "               color=\"blue\",\n",
    "               radius=14, \n",
    "               tooltip=\"$idPonto\", \n",
    "               popup=\"Lat: $lat \\nLon: $lon\",\n",
    "               fill=true,\n",
    "               fill_opacity=1).add_to(mapa)\n",
    "    idPonto += 1\n",
    "end\n",
    "\n",
    "mapa"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Modelo matemático TSP\n",
    "\n",
    "\n",
    "$\\text{Min} \\ \\ \\sum_{ij} d_{ij} x_{ij} \\\\\n",
    "\\text{Sub.} \\ \\ x_{ii} = 0 \\hspace{10em} , i = 1...n \\hspace{2.7em} \\text{# Ignora laços} \\hspace{14em} \\text{(1)} \\\\\n",
    "\\hspace{1.8em} \\sum_{i} x_{ij} = 1 \\hspace{8.8em} ,j = 1...n \\hspace{2.7em} \\text{# Apenas um arco deve chegar em j} \\hspace{6.4em} \\text{(2)}\\\\\n",
    "\\hspace{1.8em} \\sum_{j} x_{ij} = 1 \\hspace{8.8em} ,i = 1...n \\hspace{2.7em} \\text{# Apenas um arco deve sair de i} \\hspace{7.7em} \\text{(3)}\\\\\n",
    "\\hspace{1.8em} n x_{ij} - (n-1) \\leq u_{j} - u_{i} \\hspace{2.1em} ,i,j = 1...n \\hspace{2em} \\text{# Elimina subtours (Miller-Tuker-Zemlin)} \\hspace{4.2em} \\text{(4)}\\\\\n",
    "\\hspace{1.8em} x_{ij} \\in \\{0,1\\} \\\\\n",
    "\\hspace{1.8em} u_{i} \\in \\mathbb{Z} \\\\\n",
    "$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "using JuMP, CPLEX\n",
    "\n",
    "model = Model(CPLEX.Optimizer)\n",
    "\n",
    "@variable(model, x[i in 1:n, j in 1:n], Bin)\n",
    "@variable(model, u[i in 2:n])\n",
    "\n",
    "@objective(model, Min, sum(D[i,j] * x[i,j] for i in 1:n, j in 1:n))\n",
    "\n",
    "# Restrição (1)\n",
    "for i in 1:n\n",
    "    @constraint(model, x[i,i] == 0)\n",
    "end\n",
    "\n",
    "# Restrição (2)\n",
    "for j in 1:n\n",
    "    @constraint(model, sum(x[i,j] for i in 1:n) == 1)\n",
    "end\n",
    "\n",
    "# Restrição (3)\n",
    "for i in 1:n\n",
    "    @constraint(model, sum(x[i,j] for j in 1:n) == 1)\n",
    "end\n",
    "\n",
    "# Restrição (4)\n",
    "for i in 2:n\n",
    "    for j in 2:n\n",
    "        @constraint(model, n * x[i,j] - (n-1) <= u[j] - u[i])\n",
    "    end\n",
    "end\n",
    "\n",
    "# Executa o solver\n",
    "optimize!(model)\n",
    "\n",
    "# Pega valor variáveis\n",
    "x_opt = JuMP.value.(x)\n",
    "z_opt = JuMP.objective_value(model)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Exibe a solução no mapa\n",
    "for i in 1:n\n",
    "    for j in 1:n\n",
    "        if x_opt[i,j] > 0.5\n",
    "            latlonI = (LatLon[i,1],LatLon[i,2])\n",
    "            latlonJ = (LatLon[j,1],LatLon[j,2])\n",
    "            flm.PolyLine([ latlonI , latlonJ ],\n",
    "                popup=\"$z_opt metros\",\n",
    "                tooltip=\"$z_opt metros\",\n",
    "                color=\"blue\"\n",
    "            ).add_to(mapa)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "mapa"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.4.0",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
